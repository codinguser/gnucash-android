import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

def versionMajor = 2
def versionMinor = 4
def versionPatch = 1
def versionBuild = 4

static def buildTime() {
    def df = new SimpleDateFormat("yyyyMMdd HH:mm 'UTC'")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

static def gitSha() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}


android {
    compileSdkVersion 29
    buildToolsVersion '29.0.0'
    defaultConfig {
        applicationId "org.gnucash.android"
        testApplicationId 'org.gnucash.android.test'
        minSdkVersion 19
        targetSdkVersion 29
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        resValue "string", "app_version_name", "${versionName}"
        resValue "string", "app_minor_version", "${versionMinor}"
        buildConfigField "boolean", "CAN_REQUEST_RATING", "false"
        buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""

        multiDexEnabled true
        testInstrumentationRunner "org.gnucash.android.test.ui.util.GnucashAndroidTestRunner"

    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "GnucashAndroid_v${variant.versionName}.apk"
        }
    }

    signingConfigs {
        debug {
            storeFile file("../debug.keystore")
        }

        if(project.hasProperty("RELEASE_STORE_FILE")) {
            release {
                storeFile       file(RELEASE_STORE_FILE)
                storePassword   RELEASE_STORE_PASSWORD
                keyAlias        RELEASE_KEY_ALIAS
                keyPassword     RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        //todo re-enable proguard and test coverage
        release {
//            minifyEnabled true
//            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (project.hasProperty("RELEASE_STORE_FILE")){
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }
        debug {
            debuggable true
            minifyEnabled false
//            testCoverageEnabled true
            signingConfig signingConfigs.debug
        }
    }
    lintOptions {
        abortOnError false
    }

    flavorDimensions "stability"

    productFlavors {
        development {
            applicationId 'org.gnucash.android.devel'
            testApplicationId 'org.gnucash.android.devel.test'
            resValue "string", "app_name", "GnuCash-devel"
            versionName "${versionMajor}.${versionMinor}.${versionPatch}-dev${versionBuild}_r${gitSha()}"
            resValue "string", "app_version_name", "${versionName}"

            resValue "string", "dropbox_app_key", "dhjh8ke9wf05948"
            resValue "string", "dropbox_app_secret", "h2t9fphj3nr4wkw"
            resValue "string", "manifest_dropbox_app_key", "db-dhjh8ke9wf05948"
        }

        beta {
            resValue "string", "app_name", "GnuCash - beta"
            versionName "${versionMajor}.${versionMinor}.${versionPatch}-beta${versionBuild}"
            resValue "string", "app_version_name", "${versionName}"

            if (project.hasProperty("RELEASE_DROPBOX_APP_KEY")){
                resValue "string", "dropbox_app_key", RELEASE_DROPBOX_APP_KEY
                resValue "string", "dropbox_app_secret", RELEASE_DROPBOX_APP_SECRET
                resValue "string", "manifest_dropbox_app_key", "db-${RELEASE_DROPBOX_APP_KEY}"
            } else {
                resValue "string", "dropbox_app_key", "dhjh8ke9wf05948"
                resValue "string", "dropbox_app_secret", "h2t9fphj3nr4wkw"
                resValue "string", "manifest_dropbox_app_key", "db-dhjh8ke9wf05948"
            }
        }

        production {
            resValue "string", "app_name", "GnuCash"
            buildConfigField "boolean", "CAN_REQUEST_RATING", "true"

            if (project.hasProperty("RELEASE_DROPBOX_APP_KEY")){
                resValue "string", "dropbox_app_key", RELEASE_DROPBOX_APP_KEY
                resValue "string", "dropbox_app_secret", RELEASE_DROPBOX_APP_SECRET
                resValue "string", "manifest_dropbox_app_key", "db-${RELEASE_DROPBOX_APP_KEY}"
            } else {
                resValue "string", "dropbox_app_key", "dhjh8ke9wf05948"
                resValue "string", "dropbox_app_secret", "h2t9fphj3nr4wkw"
                resValue "string", "manifest_dropbox_app_key", "db-dhjh8ke9wf05948"
            }
        }
    }


    compileOptions { //we want switch with strings during xml parsing
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        animationsDisabled true
        unitTests {
            includeAndroidResources = true
        }
    }
}

/**
 * Create the Crashlytics properties file when building on CI
 * @return
 */
def initCrashlyticsPropertiesIfNeeded() {
    def propertiesFile = file('fabric.properties')
    if (!propertiesFile.exists()) {
        def commentMessage = "This is autogenerated crashlytics property from system environment to prevent key to be committed to source control."
        ant.propertyfile(file: "fabric.properties", comment: commentMessage) { //the keys added here are invalid, just placeholders to make builds pass
            entry(key: "apiSecret", value: "bd4e83a9a4c35fbf1fbe8d9ccce9443eebb9d5835605f9d06767850e0f1e5b22")
            entry(key: "apiKey", value: "46fe045d00d4ad8a71014c53567be3368e10bd64")
        }
    }
}

afterEvaluate {
    apply plugin: 'spoon'
    spoon {
        debug = true
        grantAllPermissions = true
        codeCoverage = true
    }
    initCrashlyticsPropertiesIfNeeded()
}


def androidSupportVersion       = "28.0.0"
def androidEspressoVersion      = "3.0.0"
def androidSupportTestVersion   = "1.0.0"

repositories{
    flatDir{
        dirs 'libs'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.github.nextcloud:android-library:1.0.31'
    implementation('com.android.support:support-v4:'       + androidSupportVersion,
                   'com.android.support:appcompat-v7:'     + androidSupportVersion,
                   'com.android.support:design:'           + androidSupportVersion,
                   'com.android.support:cardview-v7:'      + androidSupportVersion,
                   'com.android.support:preference-v7:'    + androidSupportVersion,
                   'com.android.support:recyclerview-v7:'  + androidSupportVersion,
                   'com.code-troopers.betterpickers:library:3.1.0',
                   'org.jraf:android-switch-backport:2.0.1@aar',
                   'com.github.PhilJay:MPAndroidChart:v2.1.3',
                   'joda-time:joda-time:2.9.4',
                   'com.google.android.gms:play-services-drive:9.6.1',
                   'io.github.kobakei:ratethisapp:1.1.3',
                   'com.squareup:android-times-square:1.6.5@aar',
                   'com.github.techfreak:wizardpager:1.0.3',
                   'net.objecthunter:exp4j:0.4.7',
                   'org.apache.jackrabbit:jackrabbit-webdav:2.13.3',
                   'com.dropbox.core:dropbox-core-sdk:3.0.3',
                   'com.android.support:multidex:1.0.1'
    )

    debugImplementation 'com.facebook.stetho:stetho:1.5.0'

    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    implementation ('com.uservoice:uservoice-android-sdk:1.2.6') {
        exclude module: 'commons-logging'
        exclude module: 'httpcore'
        exclude module: 'httpclient'
    }

    implementation('com.crashlytics.sdk.android:crashlytics:2.10.1@aar') {
        transitive = true
    }


    testImplementation 'org.robolectric:robolectric:4.5.1'

    testImplementation(
            'junit:junit:4.12',
            'joda-time:joda-time:2.9.4',
            'org.assertj:assertj-core:3.14.0'
    )
    testImplementation 'org.robolectric:shadows-multidex:4.5.1'

    androidTestImplementation (
            'com.android.support:support-annotations:'  + androidSupportVersion,
            'com.android.support.test:runner:'          + androidSupportTestVersion,
            'com.android.support.test:rules:'           + androidSupportTestVersion,
            'com.android.support.test.espresso:espresso-core:'      + androidEspressoVersion,
            'com.android.support.test.espresso:espresso-intents:'   + androidEspressoVersion,
            //the following are only added so that the app and test version both us the same versions
            'com.android.support:appcompat-v7:'     + androidSupportVersion,
            'com.android.support:design:'           + androidSupportVersion)
    androidTestImplementation ('com.android.support.test.espresso:espresso-contrib:'   + androidEspressoVersion) {
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }

    androidTestImplementation('com.squareup.assertj:assertj-android:1.1.1'){
        exclude group: 'com.android.support', module:'support-annotations'
    }

    androidTestImplementation 'com.squareup.spoon:spoon-client:1.6.4'
}
